// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© gernouille

//@version=5
indicator("Drippy's 11th hour", "11th hour", overlay=true, max_labels_count=200)
// inputs
i_eleventhStart = input.int(1050, "Start", options=[1045, 1050, 1055, 1100])
i_eleventhStop = input.int(1105, "End", options=[1105, 1110, 1115, 1120])
i_eleventhStartRev = input.int(1100, "Start", options=[1055, 1100, 1105])
i_eleventhStopRev = input.int(1120, "End", options=[1110, 1115, 1120, 1125])
i_alertOnUpTrend = input.bool (true, "Alert on uptrend")
i_alertOnDownTrend = input.bool (true, "Alert on downtrend")
i_alertOnReversal = input.bool (true, "Alert on possible reversal")
i_useMorningBGColor = input.bool( true, "Apply background color for each morning bar")

// variable & reference data
morningTime = time(timeframe.period, "0000-" + str.tostring(i_eleventhStart))
elventhHour = time(timeframe.period, str.tostring(i_eleventhStart)+"-"+str.tostring(i_eleventhStop+1))
elventhHourRev = time(timeframe.period, str.tostring(i_eleventhStartRev)+"-"+str.tostring(i_eleventhStopRev+1))
openBarIndex = ta.barssince( ((hour(time)*100) + minute(time)) < 930)

var currentTradingDay = 0
var morningHigherClose = 0.0
var morningLowerClose = 10000000.0
var firstTriggerOfTheDayOpen = 0.0
var status = "none"

if(currentTradingDay == na or currentTradingDay != time_tradingday)
    morningHigherClose := close
    morningLowerClose  := close
    firstTriggerOfTheDayOpen := 0.0
    status := "none"
    currentTradingDay := time_tradingday
    
else
    if(close>morningHigherClose)
        morningHigherClose := close
        if(time == elventhHour and barstate.isconfirmed)
            if(firstTriggerOfTheDayOpen == 0)
                firstTriggerOfTheDayOpen := open
            status := "high"
            tooltipAndAlertMsg = "New high during 11th hour ("+str.tostring(close)+">="+str.tostring(morningHigherClose)+"), day will likely trend up or close near HOD"
            label.new(bar_index, high, "H", yloc=yloc.abovebar, color=color.green, textcolor=color.green, style=label.style_triangleup, size=size.small, tooltip=tooltipAndAlertMsg)
            if(i_alertOnUpTrend)
                alert(tooltipAndAlertMsg, alert.freq_once_per_bar_close)
    if(close<morningLowerClose)
        morningLowerClose := close
        if(time == elventhHour and barstate.isconfirmed)
            if(firstTriggerOfTheDayOpen == 0)
                firstTriggerOfTheDayOpen := open
            status := "low"
            tooltipAndAlertMsg = "New low during 11th hour ("+str.tostring(close)+"<="+str.tostring(morningLowerClose)+"), day will likely trend down  or close near LOD"
            label.new(bar_index, low, "L", yloc=yloc.belowbar, color=color.red, textcolor=color.red, style=label.style_triangledown, size=size.small, tooltip=tooltipAndAlertMsg)
            if(i_alertOnDownTrend)
                alert(tooltipAndAlertMsg, alert.freq_once_per_bar_close)
    if(firstTriggerOfTheDayOpen != 0)
        if(time == elventhHourRev and barstate.isconfirmed)
            if(status == "high" and close<firstTriggerOfTheDayOpen)
                tooltipAndAlertMsg = "Possible reversal on HOD"
                label.new(bar_index, low, "R", yloc=yloc.belowbar, color=color.yellow, textcolor=color.yellow, style=label.style_diamond, size=size.small, tooltip=tooltipAndAlertMsg)
                if(i_alertOnReversal)
                    alert(tooltipAndAlertMsg, alert.freq_once_per_bar_close)
            if(status == "low" and close>firstTriggerOfTheDayOpen)
                tooltipAndAlertMsg = "Possible reversal on LOD"
                label.new(bar_index, low, "R", yloc=yloc.abovebar, color=color.yellow, textcolor=color.yellow, style=label.style_diamond, size=size.small, tooltip=tooltipAndAlertMsg)
                if(i_alertOnReversal)
                    alert(tooltipAndAlertMsg, alert.freq_once_per_bar_close)
            
        


bgcolor( i_useMorningBGColor and (time == morningTime or time == elventhHour or time == elventhHourRev) ? color.rgb(123, 123, 123, 90) : na)
bgcolor( (time == elventhHour or time == elventhHourRev) and (close>=morningHigherClose) ? color.rgb(0, 255, 0, 90) : na)
bgcolor( (time == elventhHour or time == elventhHourRev) and (close<=morningLowerClose) ? color.rgb(255, 0, 0, 90) : na)
